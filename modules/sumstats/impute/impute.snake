#!/bin/env snakemake

sys.path.insert(1, sys.path[0] + '/../../../library')
from md import process_list
from os.path import basename
methoddir = sys.path[0]

pfix = config['pfix_impute']

ld=config['--ld']
frq=f"{ld}.frq"
sumstats=config['--sumstats']
sumstats_name = basename(sumstats)
N=config['--N']
fizi = config['fizi']
Rscript = config['Rscript']

bed = process_list(config['--bed'])

format_R = f"{methoddir}/format.fizi.R"

##outputfiles
chunkout = f"{pfix}_intermediatefiles/{sumstats_name}_{{bed}}.sumstat"
chunkout_base = f"{pfix}_intermediatefiles/{sumstats_name}_{{bed}}"
chunkout_merged = f"{pfix}_finalfiles/{sumstats_name}.fizi.sumstat"
chunkout_merged_formatted = f"{pfix}_finalfiles/{sumstats_name}.fizi.sumstat.formatted"

rule fizi_all:
    input: chunkout_merged_formatted

rule fizi_one:
    input: bed = lambda wildcards: bed[wildcards.bed],
           sumstats = sumstats,
           ld = expand(f"{ld}.{{ext}}", ext = ['bed','bim','fam'])
    output: chunkout
    params: chunkout_base = chunkout_base
    shell: "{fizi} impute {input.sumstats} {ld} --locations {input.bed}"
           " --out {params.chunkout_base} --min-prop 0.01"

rule fizi_two:
    input: expand(chunkout, bed=bed)
    output: chunkout_merged
    run:
        shell("head -1 {input[0]} >> {output}")
        for i in input:
            shell("cat {i} | sed 1d >> {output}")

rule fizi_three:
    input: sumstats=chunkout_merged,
           frq=frq
    output: chunkout_merged_formatted
    params: script=format_R, N=N
    shell: "{Rscript} {params.script} {input.sumstats} {input.frq} "
           "{output} {params.N}"
